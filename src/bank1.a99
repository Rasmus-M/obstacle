***************************************************************
*
* Bank 1
*
       aorg >6000,1

***************************************************************
*
* Cartridge header
*
       byte >AA,1,1,0
       data 0
       data program_bank1
       data 0,0
program_bank1:
       data 0
       data main_bank_1
       byte 15
       text 'OBSTACLE COURSE'

***************************************************************
*
* Stub for switching to bank 0
*
main_bank_1:
       seto @bank_0

***************************************************************
*
* Unpack and copy map to VDP RAM
*
copy_map_to_vdp:
       .proc
       mov  @level,r3
       jeq  copy_map_to_vdp_1
       dec  r3
       andi r3,n_levels-1
       inc  r3
       sla  r3,2
*      Upload
copy_map_to_vdp_1:
       li   r0,>2000
       mov  @maps(r3),r1
       mov  @maps+2(r3),r2
       bl   @vmbw_bank1
*      Unpack
       li   r0,>2000                   ; Source
       li   r1,>1000                   ; Destination
       li   r2,>2000                   ; Source end
       a    @maps+2(r3),r2
       bl   @unpack_lz4_from_to_vdp
*      Rearrange
       li   r0,>1000
       li   r1,vmap0
       li   r2,>400
       li   r13,vdp_to_vdp_copy_fast_bank0
       bl   @bank_0_exec
       li   r0,>1400
       li   r1,vmap1
       li   r2,>400
       li   r13,vdp_to_vdp_copy_fast_bank0
       bl   @bank_0_exec
       li   r0,>1800
       li   r1,vmap2
       li   r2,>400
       li   r13,vdp_to_vdp_copy_fast_bank0
       bl   @bank_0_exec
       li   r0,>1c00
       li   r1,vmap3
       li   r2,>400
       li   r13,vdp_to_vdp_copy_fast_bank0
       bl   @bank_0_exec
       .endproc
*// copy_map_to_vdp

***************************************************************
*
* Copy sprite patterns to VDP RAM
*
upload_sprite_patterns:
       .proc
       li   r0,sprptb+>400
       li   r1,SPR0
       li   r2,n_sprite_patterns*32
       bl   @vmbw_bank1
       li   r0,sprptb+>400+(n_sprite_patterns*32)
       clr  r1
       li   r2,(32-n_sprite_patterns)*32
       bl   @vsmw_bank1
       .endproc
*// upload_sprite_patterns

*********************************************************************
*
* VDP multiple Byte Write
*
* R0: Write address in VDP RAM
* R1: Starting read address in CPU RAM
* R2: Number of bytes to send
*
* R0 is modified, but can be restored with: ANDI R0,>3FFF
*
vmbw_bank1:
       movb  @r0lb,@vdpwa              ; Send low byte of VDP RAM write address
       ori  r0,>4000                   ; Set read/write bits 14 and 15 to write (01)
       movb r0,@vdpwa                  ; Send high byte of VDP RAM write address
vmbw_bank1_1:
       movb *r1+,*r15                  ; Write byte to VDP RAM
       dec  r2
       jne  vmbw_bank1_1
       rt
*// vmbw_bank1

*********************************************************************
*
* VDP Single Byte Multiple Write
*
* R0: Write address in VDP RAM
* R1: MSB of R1 sent to VDP RAM
* R2: Number of bytes to write to VDP RAM
*
* R0 is modified, but can be restored with: ANDI R0,>3FFF
*
vsmw_bank1:
       movb  @r0lb,@vdpwa              ; Send low byte of VDP RAM write address
       ori  r0,>4000                   ; Set read/write bits 14 and 15 to write (01)
       movb r0,@vdpwa                  ; Send high byte of VDP RAM write address
vsmw_bank1_1:
       movb r1,*r15                    ; Write byte to VDP RAM
       dec  r2
       jne  vsmw_bank1_1
       rt
*// vsmw_bank1

*********************************************************************
*
* Includes
*
       copy "unlz4-vdp.a99"
       copy "sprite-patterns.a99"
maps:
       data map0,map0_end-map0
       data map1,map1_end-map1
       data map2,map2_end-map2
       data map3,map3_end-map3
       data map4,map4_end-map4
map0:
       bcopy "map0trim.lz4"
map0_end
       even
map1:
       bcopy "map1trim.lz4"
map1_end
       even
map2:
       bcopy "map2trim.lz4"
map2_end
       even
map3:
       bcopy "map3trim.lz4"
map3_end
       even
map4:
       bcopy "map4trim.lz4"
map4_end



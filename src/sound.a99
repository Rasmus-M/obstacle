*********************************************************************
*
* Mute sound
*
mute_sound:
       li   r0,mute_table              ; Mute all
       li   r2,4
mute_sound_1:
       movb *r0+,@sound
       dec  r2
       jne  mute_sound_1
       clr  @snd_channel_1
       clr  @snd_channel_2
       clr  @snd_channel_3
*      Return
       rt
mute_table:
       byte >9f, >bf, >df, >ff
*// mute_sound

*********************************************************************
*
* Play jump
*
play_jump:
       li   r0,snd_jump
       li   r1,snd_channel_1
       jmp  play_snd
*// play_axe

*********************************************************************
*
* Play long jump
*
play_long_jump:
       li   r0,snd_long_jump
       li   r1,snd_channel_1
       jmp  play_snd
*// play_bomb

*********************************************************************
*
* Play star
*
play_star:
       li   r0,snd_star
       li   r1,snd_channel_2
       jmp  play_snd
*// play_star

*********************************************************************
*
* Play slow
*
play_slow:
       li   r0,snd_slow
       li   r1,snd_channel_2
       jmp  play_snd
*// play_slow

*********************************************************************
*
* Play normal
*
play_normal:
       li   r0,snd_normal
       li   r1,snd_channel_2
       jmp  play_snd
*// play_normal

*********************************************************************
*
* Play die
*
play_die:
       li   r0,snd_die
       li   r1,snd_channel_3
       jmp  play_snd
*// play_die

*********************************************************************
*
* Play sound
*
* r0: Address of sound to play
* r1: Channel address
* r2: Frequency, or zero if taken from sound definition
*
* Modifies r0-r2
*
play_snd:
       clr  r2
play_snd_0:
       mov  r0,*r1+                    ; Init address
       movb *r0+,r4                    ; Operation
       movb *r0+,*r1+                  ; Init duration
       movb *r0+,*r1+                  ; Init attenuation
       inc  r0                         ; Skip attenuation step byte
       ci   r4,>e000                   ; Test for noise
       jl   play_snd_1
*      Noise - init PSG
       movb r4,@sound
       ori  r4,>1f00                   ; Change operation to full attenuation
       movb r4,@sound
       jmp  play_snd_3
*      Tone
play_snd_1:
       movb r2,r3                      ; Frequency
       jne  play_snd_2
       movb *r0+,r3                    ; Get frequency from sound definition
play_snd_2:
       movb r3,*r1                     ; Init frequency
       movb *r0,r1                     ; Frequency step
       jne  play_snd_3
*      Tone - init PSG
       movb r4,@sound
       srl  r3,2
       movb r3,@sound
       ori  r4,>1f00                   ; Change operation to full attenuation
       movb r4,@sound
play_snd_3:
       rt

*********************************************************************
*
* Sound player
*
* Modifies r0-r4
*
snd_player:
       li   r2,snd_channel_3           ; Start with last channel
snd_player_1:
       mov  *r2,r3                     ; Get sound address
       jeq  snd_player_4               ; If not active do next
       movb *r3,r4                     ; Get operation
       movb @snd_channel_duration(r2),r0     ; Are we done?
       jne  snd_player_2               ; No - jump
*      Sound finished
       clr  *r2                        ; Finished - clear sound address
       ori  r4,>1f00                   ; Full attenuation
       movb r4,@sound                  ; Mute
       jmp  snd_player_4               ; Return
snd_player_2:
*      Check noise or tone
       ci   r4,>e000                   ; Noise?
       jhe  snd_player_3               ; Yes, skip operation
       movb @snd_freq_step(r3),r0      ; Check frequency step
       jeq  snd_player_3               ; If zero skip operation
*      Frequency
       movb @snd_channel_freq(r2),r0   ; Get frequency x 4
       srl  r0,2                       ; Frequency MSB
       movb @r0lb,r1                   ; Copy least significant frequency bits
       andi r1,>c000
       srl  r1,4                       ; Shift to lower nybble
       socb r4,r1                      ; Combine with operation
       movb r1,@sound                  ; Write operation and lower frequency nybble
       movb r0,@sound                  ; Write frequency
*      Attenuation
snd_player_3:
       andi r4,>f000                   ; Clear operation lower nybble
       ori  r4,>1000                   ; Change operation to attenuation
       movb @snd_channel_attn(r2),r0   ; Get attenuation x 16
       srl  r0,4                       ; Attenuation
       socb r4,r0                      ; Combine with attenuation operation
       movb r0,@sound                  ; Write attenuation
*      Change and loop
       ab   @snd_freq_step(r3),@snd_channel_freq(r2)    ; Change frequency
       ab   @snd_attn_step(r3),@snd_channel_attn(r2)    ; Change attenuation
       sb   @snd_byte_01,@snd_channel_duration(r2)      ; Count down duration
snd_player_4:
       ai   r2,-snd_channel_size       ; Next (previous) channel
       ci   r2,snd_channel_1           ; Are we done?
       jhe  snd_player_1               ; No - loop
       rt                              ; Return
snd_byte_01:
       byte >01
**
* Sound structure
*
snd_operation:                         ; 80=T1, A0=T2, C0=T3, E4=W0, E5=W1, E6=W2
       equ  0
snd_duration:                          ; Number of frames
       equ  1
snd_attn_start:                        ; 0-255
       equ  2
snd_attn_step:                         ; Signed
       equ  3
snd_freq_start:                        ; 0-255, n/a for noise                                ;
       equ  4
snd_freq_step:                         ; Signed, n/a for noise
       equ  5

**
* Operations
*
T1     equ  >80                        ; Tone generator 1
T2     equ  >A0                        ; Tone generator 2
T3     equ  >C0                        ; Tone generator 3
W0     equ  >E4                        ; White noise type 0
W1     equ  >E5                        ; White noise type 1
W2     equ  >E6                        ; White noise type 2

**
* Note frequencies
*
A1     byte >fe
A#1    byte >f0
B1     byte >e2
C1     byte >d5
C#1    byte >c9
D1     byte >be
D#1    byte >b3
E1     byte >a9
F1     byte >a0
F#1    byte >97
G1     byte >8e
G#1    byte >86
A2     byte >7f
A#2    byte >78
B2     byte >71
C2     byte >6b
C#2    byte >65
D2     byte >5f
D#2    byte >5a
E2     byte >54
F2     byte >50
F#2    byte >4b
G2     byte >47
G#2    byte >43
A3     byte >3f
A#3    byte >3c
B3     byte >38
C3     byte >35
C#3    byte >32
D3     byte >2f
D#3    byte >2d
E3     byte >2a
F3     byte >28
F#3    byte >25
G3     byte >23
G#3    byte >21
A4     byte >1f
A#4    byte >1e
B4     byte >1c
C4     byte >1a
C#4    byte >19
D4     byte >17
D#4    byte >16
E4     byte >15
F4     byte >14
F#4    byte >13
G4     byte >11
G#4    byte >10
A5     byte >10
A#5    byte >0f
B5     byte >0e
C5     byte >0d
C#5    byte >0c
D5     byte >0c
D#5    byte >0b
E5     byte >0a
F5     byte >0a
F#5    byte >09
G5     byte >09
G#5    byte >08
A6     byte >08
A#6    byte >07
B6     byte >07
C6     byte >06
C#6    byte >06
D6     byte >06
D#6    byte >05
E6     byte >05
F6     byte >05
F#6    byte >04
G6     byte >04
G#6    byte >04

**
* Sounds
*
snd_jump:
       byte T1
       byte 16                         ; Frames
       byte 0                          ; Attn start
       byte 16                         ; Attn step
       byte 140                        ; Freq start
       byte -2                         ; Freq step
snd_long_jump:
       byte T1
       byte 32                         ; Frames
       byte 0                          ; Attn start
       byte 8                          ; Attn step
       byte 140                        ; Freq start
       byte -4                         ; Freq step
snd_star:
       byte T2
       byte 64                         ; Frames
       byte 0                          ; Attn start
       byte 4                          ; Attn step
       byte 16                         ; Freq start
       byte 0                          ; Freq step
snd_slow:
       byte T3
       byte 32                         ; Frames
       byte 0                          ; Attn start
       byte 4                          ; Attn step
       byte 16                         ; Freq start
       byte 6                          ; Freq step
snd_normal:
       byte T3
       byte 32                         ; Frames
       byte 0                          ; Attn start
       byte 4                          ; Attn step
       byte 208                        ; Freq start
       byte -6                         ; Freq step
snd_die:
       byte W0
       byte 64                         ; Frames
       byte 0                          ; Attn start
       byte 4                          ; Attn step


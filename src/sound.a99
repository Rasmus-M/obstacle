*********************************************************************
*
* Mute sound
*
mute_sound:
       li   r0,mute_table              ; Mute all
       li   r2,4
mute_sound_1:
       movb *r0+,@sound
       dec  r2
       jne  mute_sound_1
       li   r0,snd_addr_1
       clr  *r0+
       clr  *r0+
       clr  *r0+
       clr  *r0
*      Return
       rt
mute_table:
       byte >9f, >bf, >df, >ff
*// mute_sound

*********************************************************************
*
* Play jump
*
play_jump:
       li   r0,snd_jump
       clr  r1
       jmp  play_snd
*// play_axe

*********************************************************************
*
* Play long jump
*
play_long_jump:
       li   r0,snd_long_jump
       clr  r1
       jmp  play_snd
*// play_bomb

*********************************************************************
*
* Play star
*
play_star:
       li   r0,snd_star
       li r1,2
       jmp  play_snd
*// play_star

*********************************************************************
*
* Play slow
*
play_slow:
       li   r0,snd_slow
       li r1,4
       jmp  play_snd
*// play_slow

*********************************************************************
*
* Play normal
*
play_normal:
       li   r0,snd_normal
       li r1,4
       jmp  play_snd
*// play_normal

*********************************************************************
*
* Play die
*
play_die:
       li   r0,snd_die
       li   r1,6
       jmp  play_snd
*// play_die

*********************************************************************
*
* Play sound
*
* r0: Contains address of sound to play
* r1: Channel offset (0 = channel 1, 2 = channel 2, 4 = channel 3, 6 = channel 4)
*
* Modifies r0-r2
*
play_snd:
       .proc
       mov  r0,@snd_addr_1(r1)
       movb *r0+,@snd_duration_1+1(r1) ; Duration (assume MSB is zero)
       movb *r0+,r2                    ; Operation
       movb *r0+,@snd_attn_1(r1)       ; Init attenuation
       ci   r2,>e000                   ; Test for noise
       jhe  play_snd_1                 ; Yes - skip frequency
       inc  r0                         ; Skip attenuation step
       movb *r0,@snd_freq_1(r1)        ; Init frequency
play_snd_1:
       movb r2,@sound                  ; Write operation
       bl   @snd_player                ; Play first note
       .endproc
       rt

*********************************************************************
*
* Sound player
*
* Modifies r0-r4
*
snd_player:
       li   r2,6
snd_player_1:
       mov  @snd_addr_1(r2),r3         ; Is channel active?
       jeq  snd_player_4               ; No - do next
       movb @snd_operation(r3),r0      ; Get operation
       mov  @snd_duration_1(r2),r1     ; Are we done?
       jgt  snd_player_2               ; No - jump
*      Sound finished
       clr  @snd_addr_1(r2)            ; Finished - clear sound address
       ori  r0,>1f00                   ; Full attenuation
       movb r0,@sound                  ; Mute
       jmp  snd_player_4               ; Return
snd_player_2:
*      Check noise or tone
       ci   r0,>e000                   ; Noise?
       jhe  snd_player_3               ; Yes, skip operation
       mov  @snd_freq_step(r3),r1      ; Check frequency step
       jeq  snd_player_3               ; If zero skip operation
*      Frequency
       movb @snd_freq_1(r2),r1         ; Get frequency x 4
       srl  r1,2                       ; Frequency MSB
       movb @r1lb,r4                   ; Copy least significant frequency bits
       andi r4,>c000
       srl  r4,4                       ; Shift to lower nybble
       socb r4,r0                      ; Combine with operation
       movb r0,@sound                  ; Write operation and lower frequency nybble
       movb r1,@sound                  ; Write frequency
*      Attenuation
snd_player_3:
       andi r0,>f000                   ; Operation
       ori  r0,>1000                   ; Change operation to attenuation
       movb @snd_attn_1(r2),r1         ; Get attenuation x 16
       srl  r1,4                       ; Attenuation
       socb r1,r0                      ; Combine with operation
       movb r0,@sound                  ; Write attenuation
*      Change and loop
       ab   @snd_freq_step(r3),@snd_freq_1(r2)    ; Change frequency
       ab   @snd_attn_step(r3),@snd_attn_1(r2)    ; Change attenuation
       dec  @snd_duration_1(r2)        ; Count down duration
snd_player_4:
       dect r2                         ; Next channel
       jgt  snd_player_1               ; Loop
       jeq  snd_player_1               ; Loop
       rt                              ; Return

* Sound structure
snd_duration:                          ; Number of frames
       equ  0
snd_operation:                         ; 80=T1, A0=T2, C0=T3, E4=W0, E5=W1, E6=W2
       equ  1
snd_attn_start:                        ; 0-255
       equ  2
snd_attn_step:                         ; Signed
       equ  3
snd_freq_start:                        ; 0-255, n/a for noise                                ;
       equ  4
snd_freq_step:                         ; Signed, n/a for noise
       equ  5

T1     equ  >80
T2     equ  >A0
T3     equ  >C0
W0     equ  >E4
W1     equ  >E5
W2     equ  >E6

snd_jump:
       byte 16                         ; Frames
       byte T1
       byte 0                          ; Attn start
       byte 16                         ; Attn step
       byte 140                        ; Freq start
       byte -2                         ; Freq step
snd_long_jump:
       byte 32                         ; Frames
       byte T1
       byte 0                          ; Attn start
       byte 8                          ; Attn step
       byte 140                        ; Freq start
       byte -4                         ; Freq step
snd_star:
       byte 64                         ; Frames
       byte T2
       byte 0                          ; Attn start
       byte 4                          ; Attn step
       byte 16                         ; Freq start
       byte 0                          ; Freq step
snd_slow:
       byte 32                         ; Frames
       byte T3
       byte 0                          ; Attn start
       byte 4                          ; Attn step
       byte 16                         ; Freq start
       byte 6                          ; Freq step
snd_normal:
       byte 32                         ; Frames
       byte T3
       byte 0                          ; Attn start
       byte 4                          ; Attn step
       byte 208                        ; Freq start
       byte -6                         ; Freq step
snd_die:
       byte 64                         ; Frames
       byte W0
       byte 0                          ; Attn start
       byte 4                          ; Attn step

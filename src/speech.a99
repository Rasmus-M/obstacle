*********************************************************************
*
* Speak
*
speak:
       .proc
       abs  @speech_on
       jeq  speak_4
       .push r1
       mov  @speech_ptr,r1
       jeq  speak_3
       .push r0
       mov  *r1,r0
       jeq  speak_1
*      Check speech status
       bl   @get_speech_status
       movb @speech_status,r1
       andi r1,>8000
       jne  speak_2
       mov  @-6(r10),r1
       bl   @say_word
       inct @speech_ptr
       jmp  speak_2
speak_1:
       clr  @speech_ptr
speak_2:
       .pop r0
speak_3:
       .pop r1
speak_4:
       .endproc
*// speak

*********************************************************************
*
* Speak a phrase consisting of multiple words
*
* r1: points to the phrase to say.
*
say_phrase:
       .proc
       mov  *r1+,r0
       mov  r1,@speech_ptr
       bl   @say_word
       .endproc
*// say_phrase

*********************************************************************
*
* Speak a word from the resident vocabulary
*
* r0: points to the word to say.
*
say_word:
       .push r2
       abs  @speech_on
       jeq  say_word_2
       li   r2,4
say_word_1:
       src  r0,4
       mov  r0,r1
       src  r1,4
       andi r1,>0f00
       ori  r1,>4000
       movb r1,@spchwt
       dec  r2
       jne  say_word_1
       li   r0,>4000
       movb r0,@spchwt
       li   r0,>5000
       movb r0,@spchwt
say_word_2:
       .pop r2
       rt
*// say_word

*********************************************************************
*
* Speech data
*
on:
       equ  >4c41                      ; Speech on
uhoh:
       equ  >71f4                      ; Enemy appears
nice_try:
       equ  >49a5                      ; Die
partner:
       equ  >4ee0
oh:
       equ  >4b7d                      ; Die
no:
       equ  >3c4f
good_work:
       equ  >30fa                      ; Level completed
try_again:
       equ  >700f                      ; Game over
three:
       equ  >149a
two:
       equ  >145c
one:
       equ  >1409
go:
       equ  >2ffc
good:
       equ  >30d6
move:
       equ  >46df
please:
       equ  >5093
hurry:
       equ  >3757                      ; Slow
up:
       equ  >739f
back:
       equ  >1a64
again:
       equ  >17a5

three_two_one_go:
       data three,two,one,go,0
good_move:
       data good,move,0
hurry_up:
       data please,hurry,up,0
back_again:
       data back,again,0
oh_no:
       data oh,no,0
nice_try_partner:
       data nice_try,partner,0
please_try_again:
       data please,try_again,0

